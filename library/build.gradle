apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9'
    }
}

ext.getBintrayCredentials = { ->
    def bintrayUser = System.getenv('BINTRAY_USER')
    def bintrayApiKey = System.getenv('BINTRAY_API_KEY')

    if (project.rootProject.file('local.properties').exists()) {
        Properties localProperties = new Properties()
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

        bintrayUser = localProperties.getProperty('bintrayUser', bintrayUser)
        bintrayApiKey = localProperties.getProperty('bintrayApiKey', bintrayApiKey)
    }
    return [bintrayUser, bintrayApiKey]
}

def (bintrayUsername, bintrayApiKey) = getBintrayCredentials()
def VERSION = '1.0.0'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName VERSION

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

publish {
    userOrg = 'camerash'
    groupId = 'com.camerash'
    artifactId = 'filterdrawer'
    publishVersion = VERSION
    desc = 'Android plug-and-play filter that slides.'
    website = 'https://github.com/camerash/filterdrawer'
    licences = ['MIT']
    bintrayUser = bintrayUsername
    bintrayKey = bintrayApiKey
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation "com.android.support:cardview-v7:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.mikepenz:materialize:1.1.2"
    implementation "net.cachapa.expandablelayout:expandablelayout:2.9.2"
}

repositories {
    mavenCentral()
}
